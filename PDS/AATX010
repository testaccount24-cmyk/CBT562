/*REXX*****************************************************************/
/*  NAME      -  AATX010                                              */
/*  PROJECT   -  ABEND-AID TOOLS (AAT)                                */
/*  TYPE      -  REXX PROGRAM                                         */
/*  FUNCTION  -  DISPLAYS PANEL AATP010 TO COLLECT THE ABEND-TIME     */
/*               INFORMATION INCLUDING PROGRAM NAME, ABEND COMPLETION */
/*               CODE, ABEND DISPLACEMENT, AND PROGRAM LENGTH. USER   */
/*               LOCATES AND ENTERS THE HEXLOC AND INSTRUCTION NUMBER */
/*               USING THE COBOL CONDENSED LISTING AND THE DUMP.      */
/*               INFORMATION ENTERED IS VALIDATED AS POSSIBLE.        */
/*  CALLED BY -  PANEL AATP000                                        */
/*  RELATED   -  PANEL AATP010, MSG AATM01                            */
/*  CALLS     -  PANEL AATP014                                        */
/*  AUTHOR    -  DICK THORNTON JAN 2000                               */
/**********************************************************************/
/**********************************************************************/
/*  THE PROGRAM VALIDATES DATA ENTERED BY THE USER ON PANEL AATP010   */
/**********************************************************************/
/* TRACE ?ALL */
NUMERIC DIGITS 32             /* SET NUMBER DIGITS FOR CALCULATIONS */
CURSLOC = 'PROGRAM'
ERRMESG = ''
DO FOREVER
   ADDRESS ISPEXEC 'DISPLAY PANEL(AATP010)' /*DISPLAY PANEL AATP010*/
   IF RC Â¬= 0 THEN EXIT    /* ASSUME PF3 HIT IF RC IS NOT 0 */
   IF ((SUBSTR(COMP,1,1) = 'S') | (SUBSTR(COMP,1,1) = 'U')) THEN NOP
   ELSE DO
      CURSLOC = 'COMP'      /* SET CURSOR TO COMP FIELD */
      ERRMESG = 'AATM016'   /* SET ERROR MESSAGE */
      ITERATE               /* GO RE-DISPLAY */
   END
   IF DATATYPE(SUBSTR(COMP,2,3),X) THEN NOP
   ELSE DO
      CURSLOC = 'COMP'      /* SET CURSOR TO COMP FIELD */
      ERRMESG = 'AATM016'   /* SET ERROR MESSAGE */
      ITERATE               /* GO RE-DISPLAY */
   END
   DISP = X2D(ABDISP)      /* MAKE ABEND DISPLACEMENT NUMERIC  */
   PRGLN = X2D(PGMLEN)     /* MAKE PROGRAM LENGTH NUMERIC */
   ODD = DISP // 2         /* ODD = 1 IF DISP IS AN ODD NUMBER */
   IF (DISP > PRGLN) THEN DO
      CURSLOC = 'PGMLEN'    /* SET CURSOR TO PGMLEN FIELD */
      ERRMESG = 'AATM019'   /* SET ERROR MESSAGE */
      ITERATE               /* GO RE-DISPLAY */
   END
   IF ODD = 1 THEN /* ODD DISPLACEMENT IS AN ERROR */
      IF COMP = 'S0C6' THEN NOP /* (EXCEPT FOR S0C6) */
      ELSE DO
         CURSLOC = 'ABDISP'    /* SET CURSOR TO DISPLACEMENT FIELD */
         ERRMESG = 'AATM017'   /* SET ERROR MESSAGE */
         ITERATE               /* GO RE-DISPLAY */
      END
   LOCN = X2D(HEXLOC)      /* MAKE HEX LOCATION NUMERIC */
   IF DISP > LOCN THEN DO  /* HEXLOC MUST BE > DISPLACEMENT */
      IF ((DISP - LOCN) > 32) THEN DO  /* IF >32 BYTES BETWEEN */
         CURSLOC = 'HEXLOC'    /* SET CURSOR TO HEXLOC FIELD */
         ERRMESG = 'AATM018'   /* TELL USER TO CHECK */
         ITERATE               /* GO RE-DISPLAY */
      END
      ELSE DO
         CURSLOC = 'LINENO'    /* SET CURSOR TO HEXLOC FIELD */
         ADDRESS ISPEXEC 'DISPLAY PANEL(AATP014)' /*DISPLAY AATP014*/
      END
   END
   ELSE DO
      CURSLOC = 'HEXLOC'    /* SET CURSOR TO HEXLOC FIELD */
      ERRMESG = 'AATM014'   /* SET ERROR MESSAGE */
      ITERATE               /* GO RE-DISPLAY */
   END
   ADDRESS ISPEXEC "VPUT (CURSLOC ERRMESG) SHARED"
END
