/*REXX*****************************************************************/
/*  NAME      -  AATX034                                              */
/*  PROJECT   -  ABEND-AID TOOLS (AAT)                                */
/*  TYPE      -  REXX PROGRAM                                         */
/*  FUNCTION  -  VALIDATES THE CONTENT OF A DATAFIELD FOUND IN THE    */
/*               DUMP WHOSE HEX CONTENT IS GIVEN IN THE HEXDATA       */
/*               VARIABLE. VALIDITY CHECK IS MADE BASED ON THE FLDTYPE*/
/*               AND FLDLEN VARIABLES. THE DECIMAL VALUE IS PLACED IN */
/*               THE DECVAL VARIABLE FOR NUMERIC FIELDS.              */
/*  CALLED BY -  REXX PROGRAM AATX030                                 */
/*  RELATED   -  PANELS AATP034 AND MSG AATM03                        */
/*  CALLS     -                                                       */
/*  AUTHOR    -  DICK THORNTON JAN 2000                               */
/**********************************************************************/
/* TRACE ?ALL */
NUMERIC DIGITS 32             /* SET NUMBER DIGITS FOR CALCULATIONS */
ADDRESS ISPEXEC "VGET (FLDNAME FLDTYPE BLCELL " ||,
"BLCELLTY FLDOFF FLDLEN HEXDATA DECVAL ERROR  " ||,
"ERRMESG CURSLOC CHAR ZONE NUM) SHARED"
IF HEXDATA = '' THEN RETURN
HEXLEN = LENGTH(HEXDATA)
DECVAL = ''
BL = 'BL'||BLCELLTY
IF HEXLEN ¬= (2 * FLDLEN) THEN DO
   ERROR = 1
   ERRMESG = 'AATM039A'  /* INCORRECT HEX LENGTH */
   CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
   RETURN
END
IF FLDTYPE = 'B' THEN
   DECVAL = X2D(HEXDATA,HEXLEN)   /* SET UP DECIMAL EQUIVALENT */
ELSE
   IF FLDTYPE = 'P' THEN
      CALL PACKED_DECIMAL  /* VALIDATE, BUILD DECIMAL VALUE */
   ELSE
      IF FLDTYPE = 'Z' THEN
         CALL ZONED_DECIMAL /* VALIDATE, BUILD DECIMAL VALUE */
CALL CHAR_HEX_TRANSLATE    /* BUILD CHAR/ZONE/NUM EQUIVALENT */
ADDRESS ISPEXEC 'DISPLAY PANEL(AATP034)'
RETURN
/********************************************************************/
/* VALIDATE PACKED_DECIMAL FIELDS                                   */
/********************************************************************/
PACKED_DECIMAL:
DO I = 1 TO HEXLEN
   CH = SUBSTR(HEXDATA,I,1)
   IF I = HEXLEN THEN DO
      IF CH = 'D' THEN DECVAL = '-' || DECVAL
      ELSE DO
         IF CH = 'C' THEN DECVAL = '+' || DECVAL
         ELSE DO
            IF CH ¬= 'F' THEN DO
               ERROR = 1
               ERRMESG = 'AATM039B' /*INVALID PACKED SIGN */
               CURSLOC = 'HEXDATA'  /*SET CURSOR LOCATION */
               DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
               RETURN
            END
         END
      END
   END
   ELSE DO
      IF ((CH >= '0') & (CH <= '9')) THEN
         DECVAL = DECVAL || CH
      ELSE DO
         ERROR = 1
         ERRMESG = 'AATM039C' /* INVALID PACKED DIGIT */
         CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
         DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
         RETURN
      END
   END
END
RETURN
/********************************************************************/
/* VALIDATE ZONED DECIMAL FIELDS                                    */
/********************************************************************/
ZONED_DECIMAL:
DO I = 1 TO HEXLEN BY 2 while I < (HEXLEN - 1)
   ZONE = SUBSTR(HEXDATA,I,1)
   NUM  = SUBSTR(HEXDATA,I+1,1)
   IF ((NUM >= '0') & (NUM <= '9') & (ZONE = 'F')) THEN
         DECVAL = DECVAL || NUM
   ELSE DO
      ERROR = 1
      ERRMESG = 'AATM039D' /* INVALID ZONED NUMERIC */
      CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
      DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
      RETURN
   END
END
ZONE = SUBSTR(HEXDATA,HEXLEN - 1,1)
NUM  = SUBSTR(HEXDATA,HEXLEN,1)
IF ((NUM >= '0') & (NUM <= '9')) THEN
   DECVAL = DECVAL || NUM
ELSE DO
      ERROR = 1
      ERRMESG = 'AATM039D' /* INVALID ZONED NUMERIC */
      CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
      DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
      RETURN
END
IF ZONE = 'D' THEN DECVAL = '-' || DECVAL
   ELSE DO
      IF ZONE = 'C' THEN DECVAL = '+' || DECVAL
      ELSE DO
         IF ZONE ¬= 'F' THEN DO
             ERROR = 1
             ERRMESG = 'AATM039E' /* INVALID ZONED SIGN */
             CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
             DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
             RETURN
         END
      END
   END
RETURN
/********************************************************************/
/* BUILD THE CHARACTER/ZONE/NUMERIC DISPLAY                         */
/********************************************************************/
CHAR_HEX_TRANSLATE:
CHAR = ''; ZONE = ''; NUM = ''
DO I = 1 TO HEXLEN BY 2
   ZONE = ZONE || SUBSTR(HEXDATA,I,1)
   NUM  = NUM || SUBSTR(HEXDATA,I+1,1)
END
CHAR = X2C(HEXDATA)          /* CONVERT CHARACTER HEX TO REAL HEX */
CHAR = TRANSLATE(CHAR,"abcdefghi","818283848586878889"X)
CHAR = TRANSLATE(CHAR,"jklmnopqr","919293949596979899"X)
CHAR = TRANSLATE(CHAR,"stuvwxyz","A2A3A4A5A6A7A8A9"X)
ADDRESS ISPEXEC "VPUT (CHAR ZONE NUM) SHARED"
RETURN
