/*REXX*****************************************************************/
/*  NAME      -  AATX064                                              */
/*  PROJECT   -  ABEND-AID TOOLS (AAT)                                */
/*  TYPE      -  REXX PROGRAM                                         */
/*  FUNCTION  -  VALIDATES THE CONTENT OF A SUBSCRIPT FIELD FOUND IN  */
/*               THE DUMP WHOSE HEX CONTENT IS GIVEN IN THE HEXDATA   */
/*               VARIABLE. VALIDITY CHECK IS MADE BASED ON THE FLDTYPE*/
/*               AND FLDLEN VARIABLES. THE DECIMAL VALUE IS PLACED IN */
/*               THE DECVAL VARIABLE. ZERO IS INVALID FOR SUBSCRIPTS. */
/*  CALLED BY -  REXX PROGRAM AATX060                                 */
/*  RELATED   -  PANELS AATP064 AND MSG AATM06                        */
/*  CALLS     -                                                       */
/*  AUTHOR    -  DICK THORNTON JAN 2000                               */
/**********************************************************************/
/* TRACE ?ALL */
NUMERIC DIGITS 32             /* SET NUMBER DIGITS FOR CALCULATIONS */
ADDRESS ISPEXEC "VGET (FLDNAME FLDTYPE BLCELL " ||,
"BLCELLTY FLDOFF FLDLEN HEXDATA DECVAL ERROR  " ||,
"ERRMESG CURSLOC OCCLEN OCCNBR) SHARED"
ERROR = 0
HEXLEN = LENGTH(HEXDATA)
DECVAL = ''
BL = 'BL'||BLCELLTY
IF HEXLEN ¬= (2 * FLDLEN) THEN DO
   ERROR = 1
   ERRMESG = 'AATM069A'  /* INCORRECT HEX LENGTH */
   CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
END
IF ((FLDTYPE = 'B') & (ERROR = 0)) THEN
   DECVAL = X2D(HEXDATA,HEXLEN)   /* SET UP DECIMAL EQUIVALENT */
ELSE
   IF ((FLDTYPE = 'P') & (ERROR = 0)) THEN
      CALL PACKED_DECIMAL  /* VALIDATE, BUILD DECIMAL VALUE */
   ELSE
      IF ((FLDTYPE = 'Z') & (ERROR = 0)) THEN
         CALL ZONED_DECIMAL /* VALIDATE, BUILD DECIMAL VALUE */
IF ERROR = 0 THEN DO
   CALL CHAR_HEX_TRANSLATE    /* BUILD CHAR/ZONE/NUM EQUIVALENT */
   IF DECVAL ¬> 0 THEN DO
      ERROR = 1
      ERRMESG = 'AATM069F' /*SUBSCRIPT VALUE MUST BE > 0 */
      CURSLOC = 'DECVAL'
   END
   IF DECVAL > OCCNBR THEN DO
      ERROR = 1
      ERRMESG = 'AATM069G' /*SUBSCRIPT VALUE > OCCNBR */
      CURSLOC = 'DECVAL'
   END
END
IF ERROR = 0 THEN
   SUBOFF = D2X((DECVAL - 1) * OCCLEN)  /* SUBSCRIPT OFFSET */
ADDRESS ISPEXEC 'DISPLAY PANEL(AATP064)'
RETURN
/********************************************************************/
/* VALIDATE PACKED_DECIMAL FIELDS                                   */
/********************************************************************/
PACKED_DECIMAL:
DO I = 1 TO HEXLEN
   CH = SUBSTR(HEXDATA,I,1)
   IF I = HEXLEN THEN DO
      REALDEC = DECVAL           /* SAVE DECIMAL VALUE WITHOUT SIGN */
      IF CH = 'D' THEN DECVAL = '-' || DECVAL
      ELSE DO
         IF CH = 'C' THEN DECVAL = '+' || DECVAL
         ELSE DO
            IF CH ¬= 'F' THEN DO
               ERROR = 1
               ERRMESG = 'AATM069B' /*INVALID PACKED SIGN */
               CURSLOC = 'HEXDATA'  /*SET CURSOR LOCATION */
               DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
               RETURN
            END
         END
      END
   END
   ELSE DO
      IF ((CH >= '0') & (CH <= '9')) THEN
         DECVAL = DECVAL || CH
      ELSE DO
         ERROR = 1
         ERRMESG = 'AATM069C' /* INVALID PACKED DIGIT */
         CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
         DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
         RETURN
      END
   END
END
RETURN
/********************************************************************/
/* VALIDATE ZONED DECIMAL FIELDS                                    */
/********************************************************************/
ZONED_DECIMAL:
DO I = 1 TO HEXLEN BY 2 while I < (HEXLEN - 1)
   ZONE = SUBSTR(HEXDATA,I,1)
   NUM  = SUBSTR(HEXDATA,I+1,1)
   IF ((NUM >= '0') & (NUM <= '9') & (ZONE = 'F')) THEN
         DECVAL = DECVAL || NUM
   ELSE DO
      ERROR = 1
      ERRMESG = 'AATM069D' /* INVALID ZONED NUMERIC */
      CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
      DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
      RETURN
   END
END
ZONE = SUBSTR(HEXDATA,HEXLEN - 1,1)
NUM  = SUBSTR(HEXDATA,HEXLEN,1)
IF ((NUM >= '0') & (NUM <= '9')) THEN
   DECVAL = DECVAL || NUM
ELSE DO
      ERROR = 1
      ERRMESG = 'AATM069D' /* INVALID ZONED NUMERIC */
      CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
      DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
      RETURN
END
REALDEC = DECVAL           /* SAVE DECIMAL VALUE WITHOUT SIGN */
IF ZONE = 'D' THEN DECVAL = '-' || DECVAL
   ELSE DO
      IF ZONE = 'C' THEN DECVAL = '+' || DECVAL
      ELSE DO
         IF ZONE ¬= 'F' THEN DO
             ERROR = 1
             ERRMESG = 'AATM069E' /* INVALID ZONED SIGN */
             CURSLOC = 'HEXDATA'  /* SET CURSOR LOCATION */
             DECVAL = ''          /* CLEAR ANY DECIMAL VALUE */
             RETURN
         END
      END
   END
RETURN
/********************************************************************/
/* BUILD THE CHARACTER/ZONE/NUMERIC DISPLAY                         */
/********************************************************************/
CHAR_HEX_TRANSLATE:
CHAR = ''; ZONE = ''; NUM = ''
DO I = 1 TO HEXLEN BY 2
   ZONE = ZONE || SUBSTR(HEXDATA,I,1)
   NUM  = NUM || SUBSTR(HEXDATA,I+1,1)
END
CHAR = X2C(HEXDATA)          /* CONVERT CHARACTER HEX TO REAL HEX */
ADDRESS ISPEXEC "VPUT (CHAR ZONE NUM SUBOFF) SHARED"
RETURN
